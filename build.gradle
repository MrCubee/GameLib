allprojects {
    group 'fr.mrcubee.minecraft.library'
    version '1.0'
    description 'Simple mini-game library.'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'maven-publish'

    repositories {
        mavenCentral()
        maven {
            url 'https://nexus.mrcubee.net/repository/minecraft/'
        }
        maven {
            url 'https://repo.skriptlang.org/releases'
        }
    }

    dependencies {
        compileOnly 'org.spigotmc:spigot-api:1.8.8-R0.1-SNAPSHOT'
    }

    tasks.withType(JavaCompile).configureEach {
        options.compilerArgs += ["-source", "1.8", "-target", "1.8"]
    }

    compileJava.options.encoding = 'UTF-8'
    compileTestJava.options.encoding = 'UTF-8'
    processResources {
        filesMatching('**') {
            expand(
                    name: 'GameLib',
                    version: project.version,
                    url: 'https://github.com/MrCubee/GameLib',
                    description: project.description
            )
        }
    }

    publishing {
        repositories {
            maven {
                url = 'https://nexus.mrcubee.net/repository/minecraft'
                credentials {
                    username = System.getenv("MAVEN_USERNAME")
                    password = System.getenv("MAVEN_PASSWORD")
                }
            }
        }
        publications {
            shadow(MavenPublication) {
                from components.java
            }
        }
    }

}
